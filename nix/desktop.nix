# nix/desktop.nix — Hermes Desktop (Electron) app build + wrapper
#
# Returns TWO derivations: `desktop` (the regular app, pointed at the
# nix-built agent) and `light` ("Hermes Light", the remote-only client —
# no agent in its closure at all).
#
# For the regular variant, `hermesAgent` is the fully-built `.#default`
# package — it ships the `hermes` binary with the venv, runtime PATH,
# bundled skills/plugins, etc. already wired up.  We point the desktop at
# it via the existing `HERMES_DESKTOP_HERMES` override env var, so the
# desktop's resolver uses our fully wrapped binary at step 4 ("existing
# Hermes CLI").  No reimplementation of the agent resolution in this
# wrapper.  The light variant sets no agent env var: its artifact kind
# ('light' in the baked install stamp) makes the app remote-only.
{
  pkgs,
  lib,
  stdenv,
  makeWrapper,
  hermesNpmLib,
  electron,
  hermesAgent,
  python3,
  rev ? null,
  branch ? null,
  dirty ? false,
  distance ? null,
  ...
}:
let
  # The Electron manifest identifies the UI project, but Hermes's version is
  # owned by the root Python package. Keep the Nix derivation and the manifest
  # shipped to Electron aligned with that one canonical value.
  # hermes-agent.nix computes distance and displayVersion once and passes
  # them in — do not re-derive them here.
  version = (fromTOML (builtins.readFile ../pyproject.toml)).project.version;

  electronHeaders = pkgs.fetchurl {
    url = "https://artifacts.electronjs.org/headers/dist/v${electron.version}/node-v${electron.version}-headers.tar.gz";
    sha256 = "sha256-f8bSbLRmtbP93CJAvEBs+sHWDZ1xP2bcpLhC1EnOmZU=";
  };

  # node-pty ships no Electron-tagged prebuild we can trust to match this
  # exact nixpkgs electron version, so it's always compiled from source
  # against Electron's own headers (not whatever Node ran `npm`).
  targetPlatform =
    if stdenv.hostPlatform.isDarwin then
      "darwin"
    else if stdenv.hostPlatform.isLinux then
      "linux"
    else
      throw "hermes-desktop: unsupported host platform for node-pty staging";

  targetArch =
    if stdenv.hostPlatform.isAarch64 then
      "arm64"
    else if stdenv.hostPlatform.isx86_64 then
      "x64"
    else
      throw "hermes-desktop: unsupported host arch for node-pty staging";

  mkDesktop =
    {
      variant ? "",
    }:
    let
      variantSuffix = if variant != "" then "-${variant}" else "";
      binName = "hermes-desktop${variantSuffix}";

      # Build the renderer (dist/ + electron/ + package.json).
      renderer = hermesNpmLib.buildNpmPackage {
        dirs = [
          "apps/desktop"
          "apps/shared"
        ];
        pname = "${binName}-renderer";

        doCheck = true;

        # for write_install_stamp.py
        nativeBuildInputs = [
          python3
          hermesNpmLib.nodejs
        ];

        HERMES_DESKTOP_VARIANT = variant;

        buildPhase = ''
          runHook preBuild

          mkdir -p apps/desktop/build


          # grab desktopname and version, same as electron-builder
          APP_ID=${getAppId}
          node -e '
            const fs = require("fs")
            const file = "apps/desktop/package.json"
            const pkg = JSON.parse(fs.readFileSync(file, "utf8"))
            pkg.version = process.argv[1]
            pkg.desktopName = process.argv[2]
            fs.writeFileSync(file, JSON.stringify(pkg, null, 2) + "\n")
          ' '${version}' '$APP_ID'

          patchShebangs .

          pushd apps/desktop
            # typecheck :3
            npm exec -- tsc -b

            # The sandbox has no .git, so we pass things explicitly
            python3 ${../scripts/write_install_stamp.py} \
              --output build/install-stamp.json \
              ${lib.optionalString (rev != null) "--commit ${rev}"} \
              ${lib.optionalString (branch != null) "--branch ${lib.escapeShellArg branch}"} \
              ${lib.optionalString dirty "--dirty"} \
              --base-version '${version}' \
              ${lib.optionalString (distance != null) "--distance ${toString distance}"} \
              --source nix --distribution nix --update-mechanism external

            # build the renderer bundle
            # vite's emptyOutDir wipes dist/ on every run
            # so it has to be first
            npm exec -- vite build

            # build the electron bundle (bakes the stamp + identity + the
            # Linux .desktop entry, all variant-derived)
            node scripts/bundle-electron-main.mjs

            node scripts/gen-linux-desktop-entry.mjs --out-dir=build/desktop-entry

            # Compile node-pty against Electron's actual ABI (the nixpkgs
            # `electron` we ship). Headers come from a pinned fetchurl input
            # since the sandbox has no network here, so node-gyp's
            # normal --disturl download path can't run.
            mkdir -p "$TMPDIR/electron-headers"
            tar -xzf ${electronHeaders} -C "$TMPDIR/electron-headers" --strip-components=1

            ${lib.getExe hermesNpmLib.node-gyp} rebuild \
              --directory=../../node_modules/node-pty \
              --build-from-source \
              --runtime=electron \
              --target=${electron.version} \
              --nodedir="$TMPDIR/electron-headers" \
              --disturl="" \
              --offline

            # Target platform/arch come from stdenv.hostPlatform, not the
            # build host's own process.platform/arch.
            node scripts/stage-native-deps.mjs ${targetPlatform} ${targetArch}
          popd

          runHook postBuild
        '';

        checkPhase = ''
          runHook preCheck

          pushd apps/desktop

            npm run postbuild

            # validate staged node-pty native binary is present.
            STAGED_PTY_NODE="./dist/node_modules/node-pty/build/Release/pty.node"

            if [ ! -f "$STAGED_PTY_NODE" ]; then
              echo "FATAL: Missing staged node-pty native binary at $STAGED_PTY_NODE"
              echo "node-pty must be compiled natively"
              exit 1
            fi

            # The .desktop entry must exist under the variant's appId and
            # carry the placeholders the wrapper substitutes.
            APP_ID=${getAppId}
            ENTRY="build/desktop-entry/$APP_ID.desktop"
            if [ ! -f "$ENTRY" ]; then
              echo "FATAL: expected launcher entry at $ENTRY"
              ls build/desktop-entry || true
              exit 1
            fi
            grep -q '@@EXEC@@' "$ENTRY" || (echo "FATAL: $ENTRY lost its @@EXEC@@ placeholder"; exit 1)
            grep -q '@@ICON@@' "$ENTRY" || (echo "FATAL: $ENTRY lost its @@ICON@@ placeholder"; exit 1)

          popd

          runHook postCheck
        '';

        installPhase = ''
          runHook preInstall
          mkdir -p $out
          # vite writes to apps/desktop/dist/ (we cd'd there in buildPhase).
          # stage-native-deps.mjs stages node-pty into dist/node_modules/node-pty,
          # so copying dist/ wholesale carries the native dep along with the
          # esbuild bundle that require()s it. apps/desktop/build was created
          # before the cd.
          cp -rn apps/desktop/dist $out/
          cp -rn apps/desktop/build/desktop-entry $out/desktop-entry

          cp -n apps/desktop/package.json $out/
          runHook postInstall
        '';
      };
      getAppId = ''$(${lib.getExe hermesNpmLib.nodejs} -e "console.log(require('${../apps/desktop/product-identity.cjs}').appId)")'';
    in

    # Electron wrapper: nixpkgs' electron binary pointed at the renderer dir.
    stdenv.mkDerivation {
      pname = "hermes-desktop${variantSuffix}";
      inherit (renderer) version;

      HERMES_DESKTOP_VARIANT = variant;

      dontUnpack = true;
      dontBuild = true;

      nativeBuildInputs = [
        makeWrapper
      ];

      installPhase = ''
        runHook preInstall

        mkdir -p $out/share/hermes-desktop $out/bin
        cp -r ${renderer}/* $out/share/hermes-desktop/

        # Standard nixpkgs pattern for electron-builder apps: patch process.resourcesPath
        # to point to the app's directory. In Nix, unpackaged electron defaults this
        # to the electron distribution's resources path, breaking extraResources lookups.
        substituteInPlace $out/share/hermes-desktop/dist/electron-main.mjs \
          --replace-fail "process.resourcesPath" "'$out/share/hermes-desktop'"

        # Wrap the nixpkgs electron binary to launch our app.
        ${
          if variant == "light" then
            # no agent in the closure
            ''
              makeWrapper ${lib.getExe electron} $out/bin/${binName} \
                --add-flags "$out/share/hermes-desktop" \
                --set ELECTRON_IS_DEV 0
            ''
          else
            # Set HERMES_DESKTOP_HERMES to the absolute path of the
            # nix-built `hermes` binary
            ''
              makeWrapper ${lib.getExe electron} $out/bin/${binName} \
                --add-flags "$out/share/hermes-desktop" \
                --set HERMES_DESKTOP_HERMES "${lib.getExe hermesAgent}" \
                --set ELECTRON_IS_DEV 0
            ''
        }

        # XDG launcher entry — the electron-builder-generated, variant-named
        # entry from the renderer, with the placeholder Exec/Icon swapped
        # for this wrapper's store paths. Icon name matches the appId so
        # the entry's themed Icon= key resolves through hicolor.
        APP_ID=${getAppId}
        mkdir -p $out/share/applications $out/share/icons/hicolor/1024x1024/apps
        install -m 0644 ${../apps/desktop/assets/icon.png} \
          $out/share/icons/hicolor/1024x1024/apps/$APP_ID.png
        install -m 0644 ${renderer}/desktop-entry/$APP_ID.desktop \
          $out/share/applications/$APP_ID.desktop
        substituteInPlace $out/share/applications/$APP_ID.desktop \
          --replace-fail '@@EXEC@@' "$out/bin/${binName}" \
          --replace-fail '@@ICON@@' '$APP_ID'

        runHook postInstall
      '';

      passthru =
        let
          electronRuntime = with pkgs; [
            alsa-lib
            at-spi2-atk
            atk
            cairo
            cups
            dbus
            expat
            fontconfig
            freetype
            glib
            gtk3
            libdrm
            libgbm
            libxkbcommon
            mesa
            nspr
            nss
            pango
            systemd
            libX11
            libXcomposite
            libXdamage
            libXext
            libXfixes
            libXrandr
            libXrender
            libXtst
            libxcb
          ];
        in
        {
          inherit (renderer.passthru) packageJsonPath;

          devDeps = electronRuntime;
          devShellHook = ''
            export LD_LIBRARY_PATH=${lib.makeLibraryPath electronRuntime}
          '';
        };

      meta = with lib; {
        description =
          if variant == "light" then
            "Remote-only Electron desktop client for Hermes Agent"
          else
            "Native Electron desktop shell for Hermes Agent";
        homepage = "https://github.com/NousResearch/hermes-agent";
        license = licenses.mit;
        platforms = platforms.unix;
        mainProgram = binName;
      };
    };
in
{
  desktop = mkDesktop { };
  light = mkDesktop { variant = "light"; };
}

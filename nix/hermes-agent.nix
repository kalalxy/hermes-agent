# nix/hermes-agent.nix — Overridable Hermes Agent package
#
# callPackage auto-wires nixpkgs args; flake inputs are passed explicitly.
# Users override via:
#   pkgs.hermes-agent.override { extraPythonPackages = [...]; }
#   pkgs.hermes-agent.override { extraDependencyGroups = [ "hindsight" ]; }
{
  lib,
  stdenv,
  makeWrapper,
  callPackage,
  python312,
  electron,
  ripgrep,
  git,
  openssh,
  ffmpeg,
  tirith,

  # linux-only deps
  wl-clipboard,
  xclip,

  # linux-only dev deps
  cage,

  # Flake inputs — passed explicitly by packages.nix and overlays.nix
  uv2nix,
  pyproject-nix,
  pyproject-build-systems,
  npm-lockfile-fix,
  # Locked git revision of the flake source — embedded so banner.py can
  # check for updates without needing a local .git directory. Null for
  # impure / dirty builds where flakes can't determine a rev.
  rev ? null,
  revCount ? null,
  branch ? null,
  dirty ? false,
  lastModified ? null,
  # Overridable parameters
  extraPythonPackages ? [ ],
  extraDependencyGroups ? [ ],
}:
let
  version = (fromTOML (builtins.readFile ../pyproject.toml)).project.version;
  versionModule = builtins.readFile ../hermes_cli/__init__.py;
  releaseRevCountLine = lib.findFirst (line: lib.hasPrefix "__release_rev_count__" line) null (lib.splitString "\n" versionModule);
  releaseRevCountMatch = if releaseRevCountLine == null then null else builtins.match ".*= ([0-9]+)" releaseRevCountLine;
  releaseRevCount = if releaseRevCountMatch == null then null else builtins.fromJSON (builtins.elemAt releaseRevCountMatch 0);

  # Install stamp values — written to install-stamp.json so the Python
  # runtime (CLI, TUI) reads one file instead of env vars or .git probes.
  stampDistance = if revCount != null && releaseRevCount != null then lib.trivial.max 0 (revCount - releaseRevCount) else null;
  stampDisplayVersion =
    if stampDistance != null && stampDistance > 0 then "${version}+${toString stampDistance}"
    else if dirty && stampDistance == null then "${version}+?"
    else version;
  mkHermesVenv =
    extraDependencyGroups:
    callPackage ./python.nix {
      inherit uv2nix pyproject-nix pyproject-build-systems;
      pythonSrc = hermesNpmLib.pythonSrc;
      dependency-groups = [ "all" ] ++ extraDependencyGroups;
    };

  hermesVenv = (mkHermesVenv extraDependencyGroups).venv;

  hermesNpmLib = callPackage ./lib.nix {
    inherit npm-lockfile-fix;
  };

  hermesTui = callPackage ./tui.nix {
    inherit hermesNpmLib;
  };

  hermesWeb = callPackage ./web.nix {
    inherit hermesNpmLib;
  };

  bundledSkills = lib.cleanSourceWith {
    src = ../skills;
    filter = path: _type: !(lib.hasInfix "/index-cache/" path) && !(lib.hasInfix "/__pycache__/" path);
  };

  # Optional skills are NOT in the wheel (pythonSrc excludes them, see
  # lib.nix) — the wrapper exposes them via HERMES_OPTIONAL_SKILLS, the
  # same mechanism Homebrew packaging uses.
  bundledOptionalSkills = lib.cleanSourceWith {
    src = ../optional-skills;
    filter = path: _type: !(lib.hasInfix "/index-cache/" path) && !(lib.hasInfix "/__pycache__/" path);
  };

  # Import bundled plugins (memory, context_engine, platforms/*).  Keeping
  # them out of the Python site-packages keeps import semantics identical
  # to a dev checkout — the loader reads them from HERMES_BUNDLED_PLUGINS.
  bundledPlugins = lib.cleanSourceWith {
    src = ../plugins;
    filter = path: _type: !(lib.hasInfix "/__pycache__/" path);
  };

  # i18n locale catalogs (locales/*.yaml). Shipped into the store and pointed
  # at by HERMES_BUNDLED_LOCALES so the wrapped binary always resolves human
  # strings instead of raw i18n keys (#23943 / #27632 / #35374).
  bundledLocales = lib.cleanSource ../locales;

  # Shipped MCP catalog (optional-mcps/<name>/manifest.yaml). Same bare-data-dir
  # case as locales: not a Python package, so it's symlinked into the store and
  # exposed via HERMES_OPTIONAL_MCPS.
  bundledOptionalMcps = lib.cleanSourceWith {
    src = ../optional-mcps;
    filter = path: _type: !(lib.hasInfix "/__pycache__/" path);
  };

  # The managed runtime tools, built from runtime-pins.json. This is the
  # SAME table a source install and `hermes update` provision from, so a
  # nix install ships the versions the code was written against instead
  # of whatever nixpkgs happens to carry this week.
  runtimeDir = callPackage ./runtime-pins.nix { };

  # Runtime PATH. The pinned tools come first, in the dirs the bundle's
  # own `path-dirs` file names — written by runtime_env.managed_path_dirs,
  # the same assembler every Hermes subprocess uses.
  #
  # It is read in the BUILD, not here: `builtins.readFile` on a
  # derivation is import-from-derivation, which forces a full build of
  # the runtime dir during evaluation and breaks `nix flake check
  # --no-build` and cross-system eval. Nothing else in this repo does
  # IFD; a shell read in installPhase costs nothing and keeps eval pure.
  #
  # NOT lib.makeBinPath either: that appends /bin to each store path, and
  # only some of these tools keep their binary there (uv and ripgrep put
  # it at the tree root). makeBinPath silently produced dead entries for
  # those two, which is how `uv: command not found` reached the devshell.
  runtimeDeps = [
    openssh
    ffmpeg
    tirith
  ]
  ++ lib.optionals stdenv.isLinux [
    wl-clipboard
    xclip
  ];

  runtimePath = lib.makeBinPath runtimeDeps;

  sitePackagesPath = python312.sitePackages;

  # Walk propagatedBuildInputs to include transitive Python deps in PYTHONPATH.
  # Without this, a plugin listing e.g. requests as a dep would fail at runtime
  # if requests isn't already in the sealed uv2nix venv.
  allExtraPythonPackages = python312.pkgs.requiredPythonModules extraPythonPackages;

  pythonPath = lib.makeSearchPath sitePackagesPath allExtraPythonPackages;

  checkPackageCollisions = ''
    import pathlib, sys, re

    def canonical(name):
        return re.sub(r'[-_.]+', '-', name).lower()

    # Collect core venv package names
    core = set()
    venv_sp = pathlib.Path('${hermesVenv}/${sitePackagesPath}')
    for di in venv_sp.glob('*.dist-info'):
        meta = di / 'METADATA'
        if meta.exists():
            for line in meta.read_text().splitlines():
                if line.startswith('Name:'):
                    core.add(canonical(line.split(':', 1)[1].strip()))
                    break

    # Check each extra package for collisions
    extras_dirs = [${lib.concatMapStringsSep ", " (p: "'${toString p}'") allExtraPythonPackages}]
    for edir in extras_dirs:
        sp = pathlib.Path(edir) / '${sitePackagesPath}'
        if not sp.exists():
            continue
        for di in sp.glob('*.dist-info'):
            meta = di / 'METADATA'
            if not meta.exists():
                continue
            for line in meta.read_text().splitlines():
                if line.startswith('Name:'):
                    pkg = canonical(line.split(':', 1)[1].strip())
                    if pkg in core:
                        print(f'ERROR: plugin package \"{pkg}\" collides with a package in hermes sealed venv', file=sys.stderr)
                        print(f'  from: {di}', file=sys.stderr)
                        print(f'  Remove this dependency from extraPythonPackages.', file=sys.stderr)
                        sys.exit(1)
                    break

    print('No collisions found.')
  '';
in
stdenv.mkDerivation (finalAttrs: {
  pname = "hermes-agent";
  inherit version;

  dontUnpack = true;
  dontBuild = true;
  nativeBuildInputs = [ makeWrapper ];

  installPhase = ''
    runHook preInstall

    # Symlinks, not copies: these are all store paths already, and the
    # wrapper env vars just hold paths.  Symlinking keeps this derivation
    # near-instant when only the venv changed, with an identical closure.
    mkdir -p $out/share/hermes-agent $out/bin
    ln -s ${bundledSkills} $out/share/hermes-agent/skills
    ln -s ${bundledOptionalSkills} $out/share/hermes-agent/optional-skills
    ln -s ${bundledPlugins} $out/share/hermes-agent/plugins
    ln -s ${bundledLocales} $out/share/hermes-agent/locales
    ln -s ${bundledOptionalMcps} $out/share/hermes-agent/optional-mcps
    ln -s ${hermesWeb} $out/share/hermes-agent/web_dist
    ln -s ${hermesTui}/lib/hermes-tui $out/ui-tui

    # The managed runtime dir, BUILT from runtime-pins.json rather than
    # provisioned into the install root — a store path is immutable, so
    # the provisioner could never write there. Same bare-data-dir
    # treatment as the skills/locales above: symlink it in, point the
    # wrapper at it, and the Python readers consume it unchanged.
    ln -s ${runtimeDir} $out/share/hermes-agent/runtime
    ln -s ${../installation/runtime-pins.json} $out/share/hermes-agent/runtime-pins.json

    # The pinned tools' PATH dirs and tool env, as the bundle's own
    # assembler recorded them. Read here rather than in Nix so evaluation
    # stays free of import-from-derivation (see the runtimePath note
    # above). The env matters as much as PATH: dugite's git resolves its
    # helpers relative to a build-time prefix, so an unwrapped `git clone
    # https://...` fails with "'remote-http' is not a git command".
    pinnedPath=$(tr '\n' ':' < ${runtimeDir}/path-dirs)
    pinnedPath=''${pinnedPath%:}

    # Sourced, not parsed: the file is already shell (one shlex-quoted
    # `export K=V` per line), and letting the shell read it avoids a
    # hand-rolled parser for quoting the writer already handled.
    pinnedEnvArgs=()
    while IFS= read -r line; do
      key=''${line#export }
      key=''${key%%=*}
      [ -n "$key" ] || continue
      ( . ${runtimeDir}/tool-env; printf '%s' "''${!key}" ) > "$TMPDIR/envval"
      pinnedEnvArgs+=(--set-default "$key" "$(cat "$TMPDIR/envval")")
    done < ${runtimeDir}/tool-env

    # Write the canonical install stamp. version_info.py reads this at
    # runtime instead of probing env vars or .git — one file, one source
    # of truth for the Python runtime (CLI, TUI).
    cat > $out/share/hermes-agent/install-stamp.json <<STAMP
    {"schemaVersion":2,"commit":${builtins.toJSON rev},"commitDate":${builtins.toJSON lastModified},"branch":${builtins.toJSON branch},"baseVersion":"${version}","displayVersion":"${stampDisplayVersion}","distance":${builtins.toJSON stampDistance},"dirty":${if dirty then "true" else "false"},"source":"nix","distribution":"nix","updateMechanism":"external"}
    STAMP

    ${lib.concatMapStringsSep "\n"
      (name: ''
        makeWrapper ${hermesVenv}/bin/${name} $out/bin/${name} \
          --suffix PATH : "$pinnedPath:${runtimePath}" \
          "''${pinnedEnvArgs[@]}" \
          --set HERMES_BUNDLED_SKILLS $out/share/hermes-agent/skills \
          --set HERMES_OPTIONAL_SKILLS $out/share/hermes-agent/optional-skills \
          --set HERMES_BUNDLED_PLUGINS $out/share/hermes-agent/plugins \
          --set HERMES_BUNDLED_LOCALES $out/share/hermes-agent/locales \
          --set HERMES_OPTIONAL_MCPS $out/share/hermes-agent/optional-mcps \
          --set HERMES_RUNTIME_DIR $out/share/hermes-agent/runtime \
          --set HERMES_RUNTIME_PINS $out/share/hermes-agent/runtime-pins.json \
          --set HERMES_WEB_DIST $out/share/hermes-agent/web_dist \
          --set HERMES_TUI_DIR $out/ui-tui \
          --set HERMES_PYTHON ${hermesVenv}/bin/python3 \
          --set HERMES_NODE ${lib.getExe hermesNpmLib.nodejs} \
          --set HERMES_INSTALL_ROOT $out/share/hermes-agent${lib.optionalString (extraPythonPackages != [ ]) " \\
          --suffix PYTHONPATH : \"${pythonPath}\""}
      '')
      [
        "hermes"
        "hermes-agent"
        "hermes-acp"
      ]
    }

    ${lib.optionalString (extraPythonPackages != [ ]) ''
      echo "=== Checking for plugin/core package collisions ==="
      ${hermesVenv}/bin/python3 -c "${checkPackageCollisions}"
      echo "=== No collisions ==="
    ''}

    runHook postInstall
  '';

  passthru =
    let
      devPython = (mkHermesVenv (extraDependencyGroups ++ [ "dev" ])).editableVenv;
    in
    {
      inherit
        hermesTui
        hermesWeb
        hermesNpmLib
        hermesVenv
        ;

      # `hermesDesktopVariants` references `finalAttrs.finalPackage` (this
      # whole derivation, after all overrides are applied) so the regular
      # desktop wrapper can point HERMES_DESKTOP_HERMES at the fully
      # wrapped `hermes` binary — venv with all deps, bundled
      # skills/plugins, runtime PATH (ripgrep/git/ffmpeg/etc).  No
      # re-implementation of the agent resolution in the desktop wrapper.
      # The light variant carries no agent reference at all.
      hermesDesktopVariants = callPackage ./desktop.nix {
        inherit hermesNpmLib electron;
        hermesAgent = finalAttrs.finalPackage;
        inherit rev branch dirty;
        distance = stampDistance;
        displayVersion = stampDisplayVersion;
      };
      hermesDesktop = finalAttrs.finalPackage.hermesDesktopVariants.desktop;
      hermesDesktopLight = finalAttrs.finalPackage.hermesDesktopVariants.light;

      devShellHook = ''
        export HERMES_PYTHON=${devPython}/bin/python3
      '';

      devDeps =
        runtimeDeps
        ++ [
          devPython
        ]
        ++ lib.optionals stdenv.isLinux [
          cage # for running e2e tests without popping windows
        ];
    };

  meta = with lib; {
    description = "AI agent with advanced tool-calling capabilities";
    homepage = "https://github.com/NousResearch/hermes-agent";
    mainProgram = "hermes";
    license = licenses.mit;
    platforms = platforms.unix;
  };
})
